set(@PROJECT_NAME@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

set_and_check(@PROJECT_NAME@_INCLUDE_DIR "@CMAKE_INSTALL_FULL_INCLUDEDIR@")

set(_sep_deps "@SEPARATE_DEPENDENCIES@")
list(LENGTH _sep_deps _sep_deps_len)
math(EXPR len "${_sep_deps_len} - 1")

include(CMakeFindDependencyMacro)

get_filename_component(_BOOST_CMAKEDIR "${CMAKE_CURRENT_LIST_DIR}/../" ABSOLUTE)
list(APPEND CMAKE_PREFIX_PATH ${_BOOST_CMAKEDIR})

foreach(idx RANGE 0 ${len} 2)
  list(GET _sep_deps ${idx} package_name)
  math(EXPR components_idx "${idx} + 1")
  list(GET _sep_deps ${components_idx} components)
  string(REPLACE "," ";" components_list "${components}")
  if(components_list)
    find_dependency(${package_name} COMPONENTS ${components_list} REQUIRED)
  else()
    find_dependency(${package_name} REQUIRED)
  endif()
endforeach()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
check_required_components(@PROJECT_NAME@)